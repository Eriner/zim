

#
# startup file read in interactive login shells
#
# The following code helps us by optimizing the existing framework.
# This includes zcompile, zcompdump, etc.
#

(
  local zim_mods=${ZIM_HOME}/modules
  setopt EXTENDED_GLOB
  autoload -U zrecompile

  # zcompile the completion cache; siginificant speedup.
  for file in ${ZDOTDIR:-${HOME}}/.zcomp^(*.zwc)(.); do
    zrecompile -pq ${file}
  done

  # zcompile .zshrc
  zrecompile -pq ${ZDOTDIR:-${HOME}}/.zshrc

  # zcompile all module init scripts
  for file in ${zim_mods}/*/init.zsh; do
    zrecompile -pq ${file}
  done

  # zcompile all autoloaded functions
  zrecompile -pq ${ZIM_HOME}/functions ${zim_mods}/*/functions/^([_.]*|prompt_*_setup|README*|*.zwc)(.)

  # zcompile all prompt setup scripts
  for file in ${zim_mods}/prompt/functions/prompt_*_setup; do
    zrecompile -pq ${file}
  done

  # syntax-highlighting
  for file in ${zim_mods}/syntax-highlighting/external/highlighters/**^test-data/*.zsh; do
    zrecompile -pq ${file}
  done
  zrecompile -pq ${zim_mods}/syntax-highlighting/external/zsh-syntax-highlighting.zsh

  # zsh-histery-substring-search
  zrecompile -pq ${zim_mods}/history-substring-search/external/zsh-history-substring-search.zsh

) &!
